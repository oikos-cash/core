// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {IUniswapV3Pool} from "v3-core/interfaces/IUniswapV3Pool.sol";
import {LiquidityAmounts} from "v3-periphery/libraries/LiquidityAmounts.sol";

import {Uniswap} from "./Uniswap.sol";
import {Utils} from "./Utils.sol";
import {TickMath} from "v3-core/libraries/TickMath.sol";
import {IERC20Metadata} from "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {Conversions} from "./Conversions.sol";
import {IVault} from "../interfaces/IVault.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

import {
    VaultInfo,
    LiquidityPosition,
    LiquidityType,
    AmountsToMint,
    DeployLiquidityParams,
    SwapParams
} from "../types/Types.sol";
import "../errors/Errors.sol";

interface IVaultExt {
    function mintTokens(address to, uint256 amount) external returns (bool);
    function getVaultInfo() external view returns (VaultInfo memory);
}

/**
 * @title LiquidityManager
 * @notice A library for deploying and managing liquidity positions in a Uniswap V3 pool.
 */
library LiquidityDeployer {

    function deployFloor(
        address pool,
        address receiver,
        uint256 floorPrice,
        uint256 amount0ToDeploy,
        uint256 amount1ToDeploy,
        int24 tickSpacing
    ) internal returns (LiquidityPosition memory newPosition) {

        uint8 decimals = IERC20Metadata(IUniswapV3Pool(pool).token0()).decimals();
        (int24 lowerTick, int24 upperTick) = Conversions.computeSingleTick(floorPrice, tickSpacing, decimals);

        // Ensuring valid tick range
        if (upperTick <= lowerTick) {
            revert InvalidTicksFloor();
        }

        // Deploying the new liquidity position
        newPosition = deployPosition(
            DeployLiquidityParams({
                pool: pool,
                receiver: receiver,
                bips: 0,
                lowerTick: lowerTick,
                upperTick: upperTick,
                tickSpacing: tickSpacing,
                liquidityType: LiquidityType.Floor,
                amounts: AmountsToMint({
                    amount0: amount0ToDeploy,
                    amount1: amount1ToDeploy
                })
            })            
        );           
    }

    // Deploys anchor Position during initial provisioning 
    function deployAnchor(
        LiquidityPosition memory floorPosition,
        DeployLiquidityParams memory deployParams
    )
        internal
        returns (
            LiquidityPosition memory newPosition,
            LiquidityType liquidityType
        )
    {
        uint8 decimals = IERC20Metadata(IUniswapV3Pool(deployParams.pool).token0()).decimals();

        uint256 floorUpperPrice = Conversions.sqrtPriceX96ToPrice(
            Conversions.tickToSqrtPriceX96(floorPosition.upperTick),
            decimals
        );

        (int24 lowerTick, int24 upperTick) = Conversions.computeRangeTicks(
            floorUpperPrice,
            Utils.addBips(floorUpperPrice, int256(deployParams.bips)),
            deployParams.tickSpacing,
            decimals
        );

        if (upperTick <= lowerTick) {
            revert InvalidTicksAnchor();
        }

        (newPosition) = deployPosition(
            DeployLiquidityParams({
                pool: deployParams.pool,
                receiver: deployParams.receiver,
                bips: 0,
                lowerTick: lowerTick,
                upperTick: upperTick,
                tickSpacing: deployParams.tickSpacing,
                liquidityType: LiquidityType.Anchor,
                amounts: AmountsToMint({
                    amount0: deployParams.amounts.amount0,
                    amount1: deployParams.amounts.amount1
                })
            })
        );
        
        return (newPosition, LiquidityType.Anchor);
    }

    function deployDiscovery(
        uint256 upperDiscoveryPrice,
        LiquidityPosition memory anchorPosition,
        DeployLiquidityParams memory deployParams
    )
        internal
        returns (
            LiquidityPosition memory newPosition,
            LiquidityType liquidityType
        )
    {
        uint8 decimals = IERC20Metadata(address(IUniswapV3Pool(deployParams.pool).token0())).decimals();

        // uint256 lowerDiscoveryPrice = Conversions.sqrtPriceX96ToPrice(
        //     Conversions.tickToSqrtPriceX96(anchorPosition.upperTick),
        //     decimals
        // );

        // Buffer must be at least 2x tick spacing to ensure different tick after rounding
        // This makes discovery compatible with all fee tiers (including 1% with tickSpacing=200)
        // int256 minBuffer = int256(uint256(int256(anchorPosition.tickSpacing))) * 2;
        // if (minBuffer < 50) minBuffer = 50; // Minimum 0.5% buffer
        // lowerDiscoveryPrice = Utils.addBips(lowerDiscoveryPrice, minBuffer);

        uint256 lowerDiscoveryPrice = Conversions.sqrtPriceX96ToPrice(
            Conversions.tickToSqrtPriceX96(anchorPosition.upperTick),
            decimals
        );

        lowerDiscoveryPrice = Utils.addBips(lowerDiscoveryPrice, 150);

        (int24 lowerTick, int24 upperTick) = Conversions
        .computeRangeTicks(
            lowerDiscoveryPrice,
            upperDiscoveryPrice,
            anchorPosition.tickSpacing,
            decimals
        );

        if (lowerTick <= anchorPosition.upperTick) {
            revert InvalidTicksDiscovery();
        }

        uint256 balanceToken0 = IERC20Metadata(IUniswapV3Pool(deployParams.pool).token0()).balanceOf(address(this));

        newPosition = deployPosition(
            DeployLiquidityParams({
                pool: deployParams.pool,
                receiver: deployParams.receiver,
                bips: 0,
                lowerTick: lowerTick,
                upperTick: upperTick,
                tickSpacing: deployParams.tickSpacing,
                liquidityType: LiquidityType.Discovery,
                amounts: AmountsToMint({
                    amount0: balanceToken0,
                    amount1: 0
                })
            })            
        );

        newPosition.price = upperDiscoveryPrice;

        return (newPosition, LiquidityType.Discovery);
    }

    function shiftFloor(
        address pool,
        address receiver,
        uint256 newFloorPrice,
        uint256 newFloorBalance,
        LiquidityPosition memory floorPosition
    ) internal returns (LiquidityPosition memory newPosition) {       
        uint8 decimals = IERC20Metadata(IUniswapV3Pool(pool).token0()).decimals();
        (uint160 sqrtRatioX96,,,,,, ) = IUniswapV3Pool(pool).slot0();

        uint256 currentFloorPrice = Conversions.sqrtPriceX96ToPrice(
            TickMath.getSqrtRatioAtTick(floorPosition.lowerTick),
            decimals
        );

        if (newFloorPrice < currentFloorPrice) {
            newFloorPrice = currentFloorPrice;
        }

        (int24 lowerTick, int24 upperTick) =
            Conversions.computeSingleTick(
                newFloorPrice,
                floorPosition.tickSpacing,
                decimals
            );

        if (lowerTick < floorPosition.lowerTick) revert InvalidFloor();

        uint128 liquidity = LiquidityAmounts
        .getLiquidityForAmounts(
            sqrtRatioX96,
            TickMath.getSqrtRatioAtTick(lowerTick),
            TickMath.getSqrtRatioAtTick(upperTick),
            0,
            newFloorBalance
        );

        if (liquidity > 0) {

            Uniswap.mint(
                pool,
                address(this),
                lowerTick,
                upperTick,
                liquidity,
                LiquidityType.Floor,
                false
            );

            newPosition.liquidity = liquidity;
            newPosition.upperTick = upperTick;
            newPosition.lowerTick = lowerTick;
            newPosition.tickSpacing = floorPosition.tickSpacing;
            newPosition.liquidityType = LiquidityType.Floor;
            
        } else {
            revert NoLiquidity();
        }

        return newPosition;
    }

    function deployPosition(
      DeployLiquidityParams memory params
    ) internal returns (LiquidityPosition memory newPosition) {
        (uint160 sqrtRatioX96,,,,,,) = IUniswapV3Pool(params.pool).slot0();

        uint128 liquidity = LiquidityAmounts
        .getLiquidityForAmounts(
            sqrtRatioX96,
            TickMath.getSqrtRatioAtTick(params.lowerTick),
            TickMath.getSqrtRatioAtTick(params.upperTick),
            params.amounts.amount0, 
            params.amounts.amount1
        );

        if (params.liquidityType == LiquidityType.Discovery ||
            params.liquidityType == LiquidityType.Anchor) {
            if (params.amounts.amount0 == 0) {
                VaultInfo memory info = IVaultExt(params.receiver).getVaultInfo();

                uint256 toMint = params.liquidityType == LiquidityType.Discovery ?
                    info.circulatingSupply / 100000 : // 0.001% for discovery
                    info.circulatingSupply / 50000;   // 0.002% for anchor

                IVaultExt(params.receiver).mintTokens(
                    params.receiver,
                    toMint
                );

                uint256 token1Required = Uniswap
                .computeAmount1ForAmount0(
                    LiquidityPosition({
                        lowerTick: params.lowerTick, 
                        upperTick: params.upperTick, 
                        liquidity: liquidity, 
                        price: 0,
                        tickSpacing: params.tickSpacing,
                        liquidityType: params.liquidityType
                    }), 
                    toMint
                );
                
                liquidity = LiquidityAmounts
                .getLiquidityForAmounts(
                    sqrtRatioX96,
                    TickMath.getSqrtRatioAtTick(params.lowerTick),
                    TickMath.getSqrtRatioAtTick(params.upperTick),
                    toMint,
                    token1Required
                );

            }
        }

        if (liquidity > 0) {
            Uniswap.mint(
                params.pool, 
                params.receiver, 
                params.lowerTick, 
                params.upperTick, 
                liquidity, 
                params.liquidityType, 
                false
            );
        } else {
            revert NoLiquidity();
        }   

        newPosition = LiquidityPosition({
            lowerTick: params.lowerTick, 
            upperTick: params.upperTick, 
            liquidity: liquidity, 
            price: 0,
            tickSpacing: params.tickSpacing,
            liquidityType: params.liquidityType
        });          
    }

    function deployPositionRaw(
      DeployLiquidityParams memory params
    ) internal returns (LiquidityPosition memory newPosition) {
        (uint160 sqrtRatioX96,,,,,,) = IUniswapV3Pool(params.pool).slot0();

        uint128 liquidity = LiquidityAmounts
        .getLiquidityForAmounts(
            sqrtRatioX96,
            TickMath.getSqrtRatioAtTick(params.lowerTick),
            TickMath.getSqrtRatioAtTick(params.upperTick),
            params.amounts.amount0, 
            params.amounts.amount1
        );

        if (liquidity > 0) {
            Uniswap.mint(
                params.pool, 
                params.receiver, 
                params.lowerTick, 
                params.upperTick, 
                liquidity, 
                params.liquidityType, 
                false
            );
        } else {
            revert NoLiquidity();
        }   

        newPosition = LiquidityPosition({
            lowerTick: params.lowerTick, 
            upperTick: params.upperTick, 
            liquidity: liquidity, 
            price: 0,
            tickSpacing: params.tickSpacing,
            liquidityType: params.liquidityType
        });          
    }
}